#version 410

layout(triangles, equal_spacing, ccw) in;

uniform mat4 view_proj;
uniform mat4 view;
uniform mat4 model_view;
uniform mat3 normal_matrix;

out vec3 frag_pos;
out vec2 tex_coords;
out vec3 normal;
out vec3 view_dir;

struct out_patch
{
  vec3 frag_pos_B030;
  vec3 frag_pos_B021;
  vec3 frag_pos_B012;
  vec3 frag_pos_B003;
  vec3 frag_pos_B102;
  vec3 frag_pos_B201;
  vec3 frag_pos_B300;
  vec3 frag_pos_B210;
  vec3 frag_pos_B120;
  vec3 frag_pos_B111;
  vec3 normal[3];
  vec2 tex_coords[3];
};

patch in out_patch o_patch;

vec2 interpolate_2D(vec2 v0, vec2 v1, vec2 v2)
{
  return vec2(gl_TessCoord.x) * v0 + vec2(gl_TessCoord.y) * v1 + vec2(gl_TessCoord.z) * v2;
}

vec3 interpolate_3D(vec3 v0, vec3 v1, vec3 v2)
{
  return vec3(gl_TessCoord.x) * v0 + vec3(gl_TessCoord.y) * v1 + vec3(gl_TessCoord.z) * v2;
}

void main()
{
  tex_coords = interpolate_2D(o_patch.tex_coords[0], o_patch.tex_coords[1], o_patch.tex_coords[2]);
  normal = interpolate_3D(o_patch.normal[0], o_patch.normal[1], o_patch.normal[2]);
  normal = normal_matrix * normal;

  float u = gl_TessCoord.x;
  float v = gl_TessCoord.y;
  float w = gl_TessCoord.z;

  float u_pow3 = pow(u, 3);
  float v_pow3 = pow(v, 3);
  float w_pow3 = pow(w, 3);
  float u_pow2 = pow(u, 2);
  float v_pow2 = pow(v, 2);
  float w_pow2 = pow(w, 2);

  frag_pos = o_patch.frag_pos_B300 * w_pow3 +
    o_patch.frag_pos_B030 * u_pow3 +
    o_patch.frag_pos_B003 * v_pow3 +
    o_patch.frag_pos_B210 * 3.0 * w_pow2 * u +
    o_patch.frag_pos_B120 * 3.0 * w * u_pow2 +
    o_patch.frag_pos_B201 * 3.0 * w_pow2 * v +
    o_patch.frag_pos_B021 * 3.0 * u_pow2 * v +
    o_patch.frag_pos_B102 * 3.0 * w * v_pow2 +
    o_patch.frag_pos_B012 * 3.0 * u * v_pow2 +
    o_patch.frag_pos_B111 * 6.0 * w * u * v;
  vec3 tmp_frag_pos = frag_pos;

  gl_Position = view_proj * vec4(frag_pos, 1.0);
  frag_pos = (view * vec4(frag_pos, 1.0)).xyz;

  view_dir = frag_pos + normal - tmp_frag_pos;
}
